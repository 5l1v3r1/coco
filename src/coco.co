# The main entry functions for
# [tokenizing](#lexer), [parsing](#grammar), and [compiling](#nodes)
# Coco source into JavaScript.

(Coco = exports).VERSION = \0.2.1b

Lexer = require \./lexer

# The real Lexer produces a generic stream of tokens. This object provides a
# thin wrapper around it, compatible with the Jison API. We can then pass it
# directly as a "Jison lexer".
{parser} = require \./parser
parser import
  yy    : require \./nodes
  lexer :
    lex           : -> [tag, @yytext, @yylineno] = @tokens[@pos++] or ['']; tag
    setInput      : -> @pos = 0; @tokens = it
    upcomingInput : -> ''

# Compile a string of Coco code to JavaScript.
Coco.compile = (code, options) ->
  try
    (parser.parse Lexer.lex code).compileRoot options
  catch e
    e.message = "in #{that}, #{e.message}" if options?.fileName
    throw e

# Tokenize a string of Coco code, and return the array of tokens.
Coco.tokens = (code, options) -> Lexer.lex code, options

# Parse a string or tokens of Coco code, and return the AST.
Coco.nodes = (source, options) ->
  parser.parse if typeof source is \string
  then Lexer.lex source, options
  else source

return unless (fs = require \fs) and (path = require \path)

# Compile and execute a string of Coco on __node.js__, correctly
# setting `__filename`, `__dirname`, and relative `require`.
Coco.run = (code, options) ->
  root = module
  root.=parent while root.parent
  root.filename = fs.realpathSync options?.fileName || \.
  root.moduleCache &&= {}
  if require.extensions or path.extname(root.filename).toLowerCase() isnt \.co
    code = Coco.compile code, options
  root._compile code, root.filename

# Compile and evaluate a string of Coco on __node.js__.
# The Coco REPL uses this to run the input.
Coco.eval = (code, options) ->
  __dirname = path.dirname __filename = options?.fileName
  eval Coco.compile code, options

if require.extensions
  require.extensions.\.co = (module, filename) ->
    module._compile (Coco.compile fs.readFileSync filename, \utf8), filename
# __TODO__: Remove `registerExtension` when fully deprecated.
else if require.registerExtension
  require.registerExtension \.co, Coco.compile
