#### [node.js](http://nodejs.org) setups
# - Override `.run`.
# - Inherit `EventEmitter`.
# - Register _.co_ extension.

module.exports = (Coco) ->
  fs   = require \fs
  path = require \path

  Coco.run = (code, options or {}) ->
    {main} = require
    main.moduleCache &&= {}
    # Hack for relative `require`.
    filename = \.
    if options.filename
      main.paths = main.._nodeModulePaths path.dirname that
      filename = process.argv.1 = path.resolve that
    main <<< {filename}
    options.js or code = Coco.compile code, options
    main._compile code, filename

  Coco import all require(\events)EventEmitter::

  require.extensions\.co = (module, filename) ->
    module._compile do
      Coco.compile fs.readFileSync(filename, \utf8), {filename, +bare}
      filename
