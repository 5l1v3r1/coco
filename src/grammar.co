# The Coco parser is generated by [Jison](http://github.com/zaach/jison)
# from this grammar file. Jison is a bottom-up parser generator, similar in
# style to [Bison](http://www.gnu.org/software/bison), implemented in JavaScript.
# It can recognize [LALR(1), LR(0), SLR(1), and LR(1)](http://en.wikipedia.org/wiki/LR_grammar)
# type grammars. To create the Jison parser, we list the pattern to match
# on the left-hand side, and the action to take (usually the creation of syntax
# tree nodes) on the right. As the parser runs, it
# shifts tokens from our token stream, from left to right, and
# [attempts to match](http://en.wikipedia.org/wiki/Bottom-up_parsing)
# the token sequence against the rules below. When a match can be made, it
# reduces into the [nonterminal](http://en.wikipedia.org/wiki/Terminal_and_nonterminal_symbols)
# (the enclosing name at the top), and we proceed from there.
#
# If you run the `coke build:parser` command, Jison constructs a parse table
# from our rules and saves it into [lib/parser.js](../lib/parser.js).

# Jison DSL
# ---------

# Since we're going to be wrapped in a function by Jison in any case, if our
# action immediately returns a value, we can optimize by removing the function
# wrapper and just returning the value directly.
unwrap = /^function\s*\(\)\s*\{\s*return\s*([\s\S]*);\s*\}/

# Our handy DSL for Jison grammar generation, thanks to
# [Tim Caswell](http://github.com/creationix). For every rule in the grammar,
# we pass the pattern-defining string, the action to run, and extra options,
# optionally. If no action is specified, we simply pass the value of the
# previous nonterminal.
o = (patterns, action, options) ->
  patterns.=trim().split /\s+/
  action &&= unwrap.exec(action).1.replace /\b(?:[A-Z][\w.]+|mix\b)/g, 'yy.$&'
  [patterns, action or \$1, options]

# Grammatical Rules
# -----------------

# In all of the rules that follow, you'll see the name of the nonterminal as
# the key to a list of alternative matches. With each match's action, the
# dollar-sign variables are provided by Jison as references to the value of
# their numeric position, so in this rule:
#
#     "Expression MATH Expression"
#
# `$1` would be the value of the first _Expression_, `$2` would be the token
# value for the _MATH_ terminal, and `$3` would be the value of the second
# _Expression_.
bnf =
  # Everything that can be assigned to.
  Assignable: [
    o 'SimpleAssignable'
    o 'Array'
    o 'Object'
  ]

  # The types of things that can be accessed or indexed into.
  Value: [
    o 'Assignable'    ,-> Value $1
    o 'STRNUM'        ,-> Value Literal $1
    o 'Parenthetical' ,-> Value $1
    o 'THIS'          ,-> Value Literal 'this', true
    o 'LITERAL'       ,-> Value Literal $1    , true

    o 'Value CALL( ArgList OptComma )CALL' ,-> Value Call $1, $3  , $2
    o 'Value CALL(        ...       )CALL' ,-> Value Call $1, null, $2
  ]

  # Variables and properties that can be assigned to.
  SimpleAssignable: [
    o 'IDENTIFIER' ,-> Literal $1

    o 'ThisProperty'

    o 'Value DOT IDENTIFIER'           ,-> $1.add Index Key($3)     ,$2
    o 'Value DOT STRNUM'               ,-> $1.add Index Literal($3) ,$2
    o 'Value DOT Parenthetical'        ,-> $1.add Index $3.it       ,$2
    o 'Value DOT [ ArgList OptComma ]' ,-> $1.add Index $4          ,$2

    o 'Value CLONE Object' ,-> new Clone $1.unwrap(), $3

    o 'SUPER' ,-> new Super
  ]

  # All the different types of expressions in our language.
  Expression: [
    o 'Value' ,-> $1.unwrap()

    o 'Assignable ASSIGN Expression' ,-> Assign $1, $3, $2

    # Arithmetic and logical operators, working on one or two operands.
    # The precedence rules are defined at the bottom of the page.
    # It would be shorter if we could combine most of these rules into
    # a single generic "Operand OpSymbol Operand"-type rule,
    # but in order to make the precedence binding possible, separate
    # rules are necessary.
    o 'Expression +-         Expression' ,-> Op $2, $1, $3
    o 'Expression MATH       Expression' ,-> Op $2, $1, $3
    o 'Expression SHIFT      Expression' ,-> Op $2, $1, $3
    o 'Expression COMPARE    Expression' ,-> Op $2, $1, $3
    o 'Expression LOGIC      Expression' ,-> Op $2, $1, $3
    o 'Expression IMPORT     Expression' ,-> Import $1, $3, $2 is '<<<<'
    o 'Expression RELATION   Expression' ,->
      return if $2.charAt(0) is '!'
      then Op($2.slice 1; $1; $3).invert()
      else Op $2, $1, $3

    o 'UNARY Expression' , -> Op $1, $2
    o '+-    Expression' ,(-> Op $1, $2), prec: 'UNARY'

    o 'Expression ?' ,-> Existence $1

    o 'CREMENT SimpleAssignable' ,-> Op $1, $2
    o 'SimpleAssignable CREMENT' ,-> Op $2, $1, null, true

    o 'Fun'
    o 'FUNCTION Fun'            ,-> mix $2, {+statement}
    o 'FUNCTION IDENTIFIER Fun' ,-> mix $3, {+statement, name: $2}

    # The full complement of `if` expressions,
    # including postfix one-liner `if` and `unless`.
    o 'IfBlock'
    o 'Statement  POST_IF Expression' ,-> If $3, $1, name: $2
    o 'Expression POST_IF Expression' ,-> If $3, $1, name: $2

    # Comprehensions can either be normal, with a block of expressions
    # to execute, or postfix, with a single expression.
    o 'LoopHead   Block'    ,-> $1.addBody $2
    o 'Statement  LoopHead' ,-> $2.addBody Lines $1
    o 'Expression LoopHead' ,-> $2.addBody Lines $1

    o 'SWITCH Expression Cases'               ,-> new Switch $2, $3
    o 'SWITCH Expression Cases DEFAULT Block' ,-> new Switch $2, $3, $5
    o 'SWITCH Cases'                          ,-> new Switch null, $2
    o 'SWITCH Cases DEFAULT Block'            ,-> new Switch null, $2, $4

    o 'TRY Block'                        ,-> new Try $2
    o 'TRY Block CATCH IDENTIFIER Block' ,-> new Try $2, $4, $5
    o 'TRY Block
       FINALLY Block'                    ,-> new Try $2, null, null, $4
    o 'TRY Block CATCH IDENTIFIER Block
       FINALLY Block'                    ,-> new Try $2, $4, $5, $7

    # Class definitions have optional bodies of prototype property assignments,
    # and optional references to the superclass.
    o 'CLASS OptExtends'                        ,-> new Class null, $2
    o 'CLASS OptExtends Block'                  ,-> new Class null, $2, $3
    o 'CLASS SimpleAssignable OptExtends'       ,-> new Class $2, $3
    o 'CLASS SimpleAssignable OptExtends Block' ,-> new Class $2, $3, $4

    o 'SimpleAssignable EXTENDS Expression' ,-> Util.Extends $1, $3
  ]

  # Any list of statements and expressions,
  # separated by line breaks or semicolons.
  Body: [
    o 'Expression'                 ,-> Lines $1
    o 'Statement'                  ,-> Lines $1
    o 'Body TERMINATOR Expression' ,-> $1.add $3
    o 'Body TERMINATOR Statement'  ,-> $1.add $3
    o 'Body TERMINATOR'
  ]

  # An optional, trailing comma.
  OptComma: [
    o ''
    o ','
  ]

  Arg: [
    o     'Expression'
    o '... Expression' ,-> Splat $2
  ]
  # **ArgList** is either the list of objects passed into a function call,
  # the parameter list of a function, or the contents of an array literal
  # (i.e. comma-separated expressions). Newlines work as well.
  ArgList: [
    o ''                                                ,-> []
    o 'Arg'                                             ,-> [$1]
    o 'ArgList , Arg'                                   ,-> $1.concat $3
    o 'ArgList OptComma TERMINATOR Arg'                 ,-> $1.concat $4
    o 'ArgList OptComma INDENT ArgList OptComma DEDENT' ,-> $1.concat $4
  ]

  # A reference to a property on `this`.
  ThisProperty: [
    o 'THISPROP' ,-> Value Literal('this'), [Index Key $1], true
  ]

  # Parenthetical expressions. Note that the **Parenthetical** is a **Value**,
  # not an **Expression**, so if you need to use an expression in a place
  # where only values are accepted, wrapping it in parentheses will always do
  # the trick.
  Parenthetical: [
    o '(        Body        )' ,-> Parens $2.unwrap()
    o '( INDENT Body DEDENT )' ,-> Parens $3.unwrap()
  ]

  # Pure statements which cannot be expressions.
  Statement: [
    o 'RETURN Expression' ,-> Return $2
    o 'RETURN'            ,-> Return()
    o 'STATEMENT'         ,-> new Statement $1
    o 'THROW Expression'  ,-> new Throw $2
    o 'COMMENT'           ,-> Comment $1
  ]

  # An indented block of expressions. Note that the [Rewriter](#rewriter)
  # will convert some postfix forms into blocks for us, by adjusting the
  # token stream.
  Block: [
    o 'INDENT Body DEDENT' ,-> $2
    o 'INDENT      DEDENT' ,-> Lines()
  ]

  # **Fun** node is the function literal, defined by an indented **Block**
  # preceded by a function arrow, with an optional parameter list.
  Fun: [
    o 'PARAM( ArgList OptComma )PARAM FUNC_ARROW Block' ,-> Fun $2, $6, $5
    o 'FUNC_ARROW Block'                                ,-> Fun [], $2, $1
  ]

  Array: [
    o '[ ArgList OptComma ]' ,-> Arr $2
  ]

  # Things that can be keys within an object literal.
  ObjAssignable: [
    o 'IDENTIFIER' ,-> Literal $1
    o 'STRNUM'     ,-> Literal $1
    o 'Parenthetical'
    o 'ThisProperty'
  ]
  AssignObj: [
    o 'ObjAssignable :        Expression'        ,-> Assign $1.asKey(), $3, ':'
    o 'ObjAssignable : INDENT Expression DEDENT' ,-> Assign $1.asKey(), $4, ':'

    o     'ObjAssignable' ,-> $1
    o '... ObjAssignable' ,-> Splat  $2
    o '+-  ObjAssignable' ,-> Assign $2, Literal($1 is '+'), ':'

    o 'COMMENT' ,-> Comment $1
  ]
  # Assignment of properties within an object literal can be separated by
  # comma, as in JavaScript, or simply by newline.
  AssignList: [
    o ''                                                      ,-> []
    o 'AssignObj'                                             ,-> [$1]
    o 'AssignList , AssignObj'                                ,-> $1.concat $3
    o 'AssignList OptComma TERMINATOR AssignObj'              ,-> $1.concat $4
    o 'AssignList OptComma INDENT AssignList OptComma DEDENT' ,-> $1.concat $4
  ]
  # In Coco, an object literal is simply a list of assignments.
  Object: [
    o '{ AssignList OptComma }' ,-> Obj $2
  ]

  # The most basic form of *if* is a condition and an action. The following
  # if-related rules are broken up along these lines in order to avoid
  # ambiguity.
  IfBlock: [
    o 'IF Expression Block'              ,-> If $2, $3, name: $1
    o 'IfBlock ELSE IF Expression Block' ,-> $1.addElse If $4, $5, name: $3
    o 'IfBlock ELSE Block'               ,-> $1.addElse $3
  ]

  LoopHead: [
    # The source of a `for`-loop is an array, object or range.
    # Unless it's iterating over an object, you can choose to step through
    # in fixed-size increments.
    o 'FOR Assignable FOROF Expression'
    , -> mix new For, name: $2, index: $3, source: $4
    o 'FOR Assignable FOROF Expression BY Expression'
    , -> mix new For, name: $2, index: $3, source: $4, step: $6

    o 'FOR IDENTIFIER              FORIN Expression'
    , -> mix new For, object: true, own: !$1, index: $2,           source: $4
    o 'FOR IDENTIFIER , Assignable FORIN Expression'
    , -> mix new For, object: true, own: !$1, index: $2, name: $4, source: $6

    o 'FOR IDENTIFIER FROM Expression TO Expression'
    , -> mix new For, index: $2, from: $4, op: $5, to: $6
    o 'FOR IDENTIFIER FROM Expression TO Expression BY Expression'
    , -> mix new For, index: $2, from: $4, op: $5, to: $6, step : $8

    o 'WHILE Expression' ,-> new While $2, $1
    o 'FOR EVER'         ,-> new While
  ]

  Cases: [
    o       'CASE Expression Block' ,-> [new Case $2, $3]
    o 'Cases CASE Expression Block' ,-> $1.concat new Case $3, $4
  ]

  # Optional `extends` clause for class definition.
  OptExtends: [
    o ''              ,-> null
    o 'EXTENDS Value' ,-> $2
  ]

  # The **Root** is the top-level node in the syntax tree.
  # Since we parse bottom-up, all parsing must end here.
  Root: [
    o '' ,-> Lines()
    o 'Body'
    o 'Block TERMINATOR'
  ]

# Precedence
# ----------
# Operators at the top of this list have higher precedence than the ones lower
# down as in [Yacc](http://dinosaur.compilertools.net/yacc/index.html).
# Following these rules is what makes `2 + 3 * 4` parse as:
#
#     2 + (3 * 4)
#
# And not:
#
#     (2 + 3) * 4
operators = [
  <[ nonassoc  CREMENT      ]>
  <[ left      ?            ]>
  <[ right     UNARY        ]>
  <[ left      MATH         ]>
  <[ left      +-           ]>
  <[ left      SHIFT IMPORT ]>
  <[ left      RELATION     ]>
  <[ left      COMPARE      ]>
  <[ left      LOGIC        ]>
  <[ right     : ASSIGN COMPOUND_ASSIGN RETURN THROW EXTENDS INDENT     ]>
  <[ right     IF ELSE SWITCH CASE DEFAULT CLASS FORIN FOROF FROM TO BY ]>
  <[ left      POST_IF FOR WHILE ]>
# Reverse them because Jison orders precedence from low to high
].reverse()

# Wrapping Up
# -----------

# Finally, now what we have our **bnf** and our **operators**, we can create
# our **Jison.Parser**. We do this by processing all of our rules, recording all
# terminals (every symbol which does not appear as the name of a rule above)
# as **tokens**.
tokens = for name, alternatives in bnf
  for alt of alternatives
    alt.1 = "#{ if name is \Root then \return else '$$ =' } #{alt.1};"
    token if token not in bnf for token of alt.0
tokens.=join ' '

# Initialize the **Parser** with our **bnf**, **operators**,
# our list of terminal **tokens**, and the name of the root.
exports.parser =
  new (require \jison).Parser {bnf, operators, tokens, startSymbol: \Root}
