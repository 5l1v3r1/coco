# Coco language plugin for
# [google-code-prettify](http://code.google.com/p/google-code-prettify/).
tint = (ext, shortcuts, fallthroughs) ->
  rule.splice 2 0 0 if rule.length < 4 for rule of shortcuts
  PR.registerLangHandler PR.createSimpleLexer(shortcuts, fallthroughs), [ext]

ident = '(?:[$A-Za-z_\\x7f-\\uffff][$\\w\\x7f-\\uffff]*)'
kwend = '(?![$\\w\\x7f-\\uffff])'

### Main
tint \co [
  [\str /// ^ '(?: ''[\S\s]*?'' | [^\\']*(?:\\.[^\\']*)* )' /// \']
  [\lang-co-qq ///(^ " (?: ""[\S\s]*?"" | [^\\"]*(?:\\.[^\\"]*)* ) " )/// \"]
  [\lang-co-qr ///(^ /{3} [\S\s]*? /{3} (?:\?|[imgy]{0,4}) )/// \/]
  [\lang-co-at ///(^ @@? #{ident}? )/// \@]
  [\com /^#.*/ \#]
  [\typ /// ^ (?
  : 0x[\da-f]+
  | [1-9]\d? r [\da-z]+
  | (?:\d+(?:\.\d+)?) (?:e[+-]?\d+)? [a-z_]*
  ) ///i \0123456789]
  [\lang-js /^`([^\\`]*(?:\\.[^\\`]*)*)`/ \`]
] [
  [\str /// ^ \\ \S [^\s,;)}\]]* ///]
  [\com /// ^ /\* [\S\s]* \*/ ///]
  [\pln /// ^ (?
  : \.{3}
  | (?: [?~]?\.\s* | [)}\]] | :: ) #{ident}?
  | #{ident} [^\n\S]* :(?!:)
  ) ///]
  # ref. [retrie](https://github.com/satyr/retrie)
  [\kwd /// ^ (?
  : t(?:ry|h(?:row|en)|ypeof)
  | f(?:or(?: [^\n\S]+(?:own|ever) )?|inally|unction)
  | n(?:ew|ot)
  | c(?:ontinue|a(?:se|tch)|lass)
  | i(?:[fs]|n(?:stanceof)?|mport(?:[^\n\S]+ all)?)
  | e(?:lse|xtends)
  | d(?:e(?:fault|lete|bugger)|o)
  | un(?:less|til)
  | o[fr] | return | break | while | switch | and
  ) #{kwend} ///]
  [\typ /// ^ (?: true | false | null | void ) #{kwend} ///]
  [\ctx /// ^ (?
  : t(?:h(?:is|at)|o|il)
  | f(?:rom|allthrough)
  | it | arguments | eval | super | by
  ) #{kwend} ///]
  [\glb /// ^ (?
  : Array | Boolean | Date | Error | Function | JSON | Math | Number
  | Object | RegExp | S(?:tring|yntaxError) | TypeError
  ) #{kwend} ///]
  [\var /// ^ #{ident} ///]
  [\str /^<\[[\S\s]*?]>/]
  [\lang-co-r /// ^ [^/] (
    / (?![\s/])
    [^ [ / \n \\ ]*
    (?:
      (?: \\.
        | \[ [^\]\n\\]*(?:\\.[^\]\n\\]*)* \]
      ) [^ [ / \n \\ ]*
    )* / [imgy]{0,4}
  ) (?!\d) ///]
]

### Subrules
hashbraces = [\lang-co /^#({[\S\s]*?})/ \#]
regexes    = [\lit /^[\S\s]+?/]

tint \co-qq [hashbraces] [[\str /^[\S\s]+?/]]
tint \co-qr [hashbraces] [[\com /^\s#(?!{).*/] regexes]
tint \co-r  [] [regexes]

tint \co-at [[\ctx /^@+/ \@]] []
