# Coco language plugin for
# [google-code-prettify](http://code.google.com/p/google-code-prettify/).
tint = (ext, shortcuts, fallthroughs) ->
  rule.splice 2 0 0 if rule.length < 4 for rule of shortcuts
  PR.registerLangHandler PR.createSimpleLexer(shortcuts, fallthroughs), [ext]

### Main
tint \co, [
  [\str /// ^ ' (?: ''[\S\s]*?'' | [^\\']*(?:\\.[^\\']*)* ) ' /// \']
  [\lang-co-qq /^("""[\S\s]*?""")/ \"]
  [\lang-co-qq /// ^ ( " [^\\"]*(?:\\.[^\\"]*)* " ) /// \"]
  [\lang-co-qr /// ^ ( /{3} [\S\s]+? /{3} [imgy]{0,4} ) (?!\w) /// \/]
  [\lang-co-at /^(@+[$\w\x7f-\uffff]*)/ \@]
  [\com /^\#.*/ \#]
  [\typ /// ^ (?
  : 0x[\da-f]+
  | [1-9]\d? r [\da-z]+
  | (?:\d+(?:\.\d+)?) (?:e[+-]?\d+)? [a-z]*
  ) ///i \0123456789]
  [\lang-js /^`([^\\`]*(?:\\.[^\\`]*)*)`/ \`]
], [
  [\str /// ^ \\ \S [^\s,;)}\]]* ///]
  [\com /// ^ /\* [\S\s]* \*/ ///]
  # ref. [retrie](https://github.com/satyr/retrie)
  [\pln /// ^ (?
  : (?: \@ | (?:\.|::) \s* ) [$A-Za-z_\x7f-\uffff][$\w\x7f-\uffff]*
  | [$A-Za-z_\x7f-\uffff][$\w\x7f-\uffff]* [^\n\S]* : (?!:)
  | \.{3}
  ) ///]
  [\kwd /// ^ (?
  : t(?:ry|h(?:row|en)|ypeof)
  | f(?:or(?:[^\n\S]+ (?:own|ever))?|inally|unction)
  | n(?:ew|ot)
  | c(?:ontinue|a(?:se|tch)|lass)
  | i(?:[fs]|n(?:stanceof)?|mport(?:[^\n\S]+ all)?)
  | e(?:lse|xtends)
  | d(?:e(?:fault|lete|bugger)|o)
  | un(?:less|til)
  | o[fr] | return | break | while | switch | and
  ) \b ///]
  [\typ /// ^ (?: true | false | null | void ) \b ///]
  [\ctx /// ^ (?
  : t(?:h(?:is|at)|o|il)
  | f(?:rom|allthrough)
  | it | arguments | eval | super | by
  ) \b ///]
  [\glb /// ^ (?
  : Array | Boolean | Date | Error | Function | JSON | Math | Number
  | Object | RegExp | S(?:tring|yntaxError) | TypeError
  ) \b(?![^\n\S]*:(?!:)) ///]
  [\var /^[$A-Za-z_\x7f-\uffff][$\w\x7f-\uffff]*/]
  [\str /^<\[[\S\s]*?]>/]
  [\lang-co-r /// ^ [^/] ( / (?![\s/])
    [^ [ / \n \\ ]*
    (?:
      (?
      : \\[\S\s]
      | \[
          [^ \] \n \\ ]*
          (?: \\[\S\s] [^ \] \n \\ ]* )*
         ]
      ) [^ [ / \n \\ ]*
    )* / [imgy]{0,4} ) (?!\w)
  ///]
]

### Subrules
hashbraces = [\lang-co /^#({[\S\s]*?})/ \#]
regexes    = [\lit /^[\S\s]+?/]

tint \co-qq, [hashbraces], [[\str /^[\S\s]+?/]]
tint \co-qr, [hashbraces], [[\com /^\s#(?!{).*/], regexes]
tint \co-r , [], [regexes]

tint \co-at, [[\ctx /^@+/ \@]], [[\typ /^\d+/]]
