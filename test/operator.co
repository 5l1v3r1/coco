# Newline suppression for binary operators.
eq 32,
  1 *
  2 +
  3 -
  4 <<
  5


# Chained Comparisons
ok 500 > 50 > 5 > -5

ok 0 is 0 is not 50 is 50

ok 10 < 20 > 10

ok 50 > 10 > 5 is parseInt('5', 10)

eq 1, 1 | 2 < 3 < 4

ok 1 == 1 <= 1, '`x == y <= z` should become `x == y && y <= z`'

i = 0
ok 1 > i++ < 1, 'chained operations should evaluate each value only once'

# [coffee#891](https://github.com/jashkenas/coffee-script/issues/891)
eq 1, (1 unless 1 < 0 == 0)


# (In)equality
a = 1
b = '1'
ok a == b
ok not a != b
ok not (a === b)
ok not (a is  b)
ok a !==  b
ok a is not b

ok true is     true
ok true is not false


# `and`/`or` closes implicit calls.
eq('1', String 0 and String 1)
eq('0', String 0 or  String 1)


ok 'a' in obj = a: true
ok 'b' not in obj, 'allow `x not in y`'


ok new String instanceof String
ok new Number not instanceof String, 'allow `x not instanceof Y`'

eq true, []    instanceof [String, Number and Array]
eq true, 0 not instanceof [String, Array  or Number]


eq true, 200 of [0 or 100, 200, 300]
eq true, 200 of array = [100, 200, 300]
eq true, 1 not of array
eq true, 1 not of []
ok array[0]++ of [99, 100], 'should cache testee'


# Non-spaced values still work.
x = 10
y = -5
eq x-9, 1
eq y+9, 4
eq x*-y, 50
eq x*+y, -50


# Conditional assignments.
one = 1
two = 0
one || = 2
two || = 2

eq one, 1
eq two, 2

zero = 0
zero &&= 'one'
one  &&= 'one'

eq zero, 0
eq one , 'one'

# Conditional assignments should be careful about caching variables.
count = 0
list = []

list[++count] ||= 1
eq list[1], 1
eq count, 1

list[++count] ?= 2
eq list[2], 2
eq count, 2

list[count++] &&= 'two'
eq list[2], 'two'
eq count, 3

base = -> ++count; base

base().four ||= 4
eq base.four, 4
eq count, 4

base().five ?= 5
eq base.five, 5
eq count, 5

# Ensure that RHS is treated as a group.
a = b = false
a &&= b or true
eq a, false

# Conditional assignments with implicit objects.
obj = void
obj ?= one: 1
eq obj.one, 1

obj &&=
  two: 2

ok not obj.one
eq obj.two, 2


# Compound assignment as a sub expression.
[a, b, c] = [1, 2, 3]
eq (a + b += c), 6
eq a, 1
eq b, 5
eq c, 3


# Bitwise operators:
eq (10 &   3), 2
eq (10 |   3), 11
eq (10 ^   3), 9
eq (10 <<  3), 80
eq (10 >>  3), 1
eq (10 >>> 3), 1

num = 10; eq (num <<=  3), 80
num = 10; eq (num >>=  3), 1
num = 10; eq (num >>>= 3), 1
num = 10; eq (num &=   3), 2
num = 10; eq (num ^=   3), 9
num = 10; eq (num |=   3), 11


#coffee-737: `in` should have higher precedence than logical operators.
eq 1, 1 of [1] && 1

#coffee-768: `in` should preserve evaluation order.
share = 0
a = -> share++ if share is 0
b = -> share++ if share is 1
c = -> share++ if share is 2
ok a() not of [b(),c()] and share is 3

# `in` with cache and `__indexOf` should work in commaed lists.
eq [Object() of Array()].length, 1


# Operators should respect new lines as spaced.
a = (123) +
456
eq a, 579

a = "1#{2}3" +
"456"
eq a, '123456'


# Multiple unary {plus,minus}es should space themselves.
eq(+ +1, - -1)


# `do`
eq '', do do do -> -> -> do String
eq 1, do -> 1
eq @, do ~> @

eq 1, do then 0; 1
eq 3, do
  2
  3

a = Array do
  a: 0, b: 1
  2, 3
eq 1, a.0.b
eq 3, a.2

fn = null
eq void fn? do then 4;5


### `import`
x = 'xx'
o = (-> {} import {42, '', x, @X, (x), ...([0])}).call {X: 'XX'}
eq o[42], 42
eq o[''], ''
eq o.x, 'xx'
eq o.X, 'XX'
eq o.xx, 'xx'
eq o[0], 0

o import all new class then deep: 'copy'
eq o.deep, 'copy'

o import all: \base
eq o.all, \base

a = [0]
a import (*): 1, (*): 2
eq a[1], 1
eq a[2], 2

i = 0
++i import {}
eq i, 1

x = {}; a = 0; b = 1; c = null; i = 0
x <<< {a || 1, b && 2, c ? 3, (i++) or 4}
eq x.a, 1
eq x.b, 2
eq x.c, 3
eq x.0, 4

eq ',1,2,3' "#{ [] <<< [void 1 2 3] }"
eq ',1,2,3' "#{ [] <<< {0}{1} <<<< {2}{3} }"

eq '''
({
  a: a,
  b: b,
  c: c
});
''', Coco.compile '{a}<<<{b}<<<{c}', {+bare}

ok ok.isPrototypeOf new []= (->) <<< prototype: ok


### `delete`
i = 0
O = ->
  switch ++i
  case 1 then {7}
  case 2 then new String 7
  default ok 0, 'returning delete should cache correctly'
eq delete (o = new O)[new O], 7
eq o[7], void

throws 'invalid deletion on line 1' -> Coco.compile 'delete a'
throws 'invalid deletion on line 1' -> Coco.compile 'delete 0'


### [[Class]] sniffing
eq \RegExp typeof! /^/


### Pow
eq 64, -2**2**3  # ((-2)**2)**3
eq 65, 1+2*2**3  # 1+((2*2)**3)

a = [2]; i = 0
a[i++] **= 3
eq a.0, 8


## Overloaded
a = [0 1]
b = ''

### Concat
eq '0,1,2' String a + [2]
eq '3,4,5' String [3 4] + {0: 5, length: 1}
eq '0,1' ''+ b += [0 1]
ok b instanceof a..

### Join
eq '0==1' a * \==
eq '0101' [... a] * 2 * ''
eq '(01)' <[( )]> * "#{a * ''}"
eq '0@@1' (-> @@ * \@@) 0 1
eq '0.1' b *= \.
eq '0.1' b

### Split
eq '0,1' String a / \,
eq 2 (/abc/ / /[^/]+/)length
eq "#{ x = ''+ Math.random() }"/'.'*'.' x
eq '0,1' ''+ b /= /\D/
eq '0,1' ''+ b
