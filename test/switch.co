switch 10
case 5 then ok 0
case 'a'
  true
  false
  ok 0
case 10 then ok 1

  #! Mid-switch comment with whitespace

    #! and multi line
case 11 then ok 0
default ok 0


func = (num) ->
  switch num
  case 2, 4, 6
    Boolean true
  case [1, 3, 5]
    Boolean false
  default
eq func(2), true
eq func(6), true
eq func(3), false
eq func(8), void


# One-liner
eq void, switch case 1 then break
eq 1   , switch case 0 then break default 1
eq 2   , switch case 1 then continue while 0; 2
eq 3   , do -> switch -> 0 case 1 then -> 2 default 3


throws 'inconvertible statement on line 3'
, -> Coco.compile '''
  1 + switch
  case 2
    continue
'''


result = switch 'words'
case (<[ nonbare words ]>) then false
case  <[    bare words ]>  then true
default false
ok result, '`case`s can take bare arrays'


# Sans-topic
eq ok, switch
case null                    then 0
case !1                      then 1
case '' not in {''}          then 2
case [] not instanceof Array then 3
case true is false           then 4
case 'x' < 'y' > 'z'         then 5
case 'a' of  <[ b c ]>       then 6
case 'd' of (<[ e f ]>)      then 7
default ok


eq '''
var that;
switch (false) {
case !1:
  return;
case !2:
  throw me;
case !3:
  continue;
case !4:
  break;
case !5:
  // fallthrough
case !(that = 6):
  that;
  break;
case !(7 || 8 || 9):
  break;
case !void 8:
  break;
default:
  8;
}
''', Coco.compile '''
switch
case 1 then return
case 2 then throw me
case 3 then continue
case 4 then break
case 5 then fallthrough
case 6 then that
case 7 [8 9] then
case[] then
default 8
''', {+bare}


# `that`
eq 1, switch 1 case 1 then that

while 1
  eq 3, switch 3 case 3 then that
  break

switch case [0, 2, 4,] then eq 2, that


# Sans-condition
switch
  ok 1 'caseless switch is allowed'
  break if true
  ok 0 'for early breaking'
