if a = b = d = true
  c = false

result = if a
  if b
    if c then false else
      if d
        true
ok result


ok if false then false else v = if false then false else true
ok v


result = if false
  false
else if NaN
  false
else
  true

ok result


# `unless`
result = unless true
  10
else
  11

eq 11, result


# Nested inline if statements.
eq 'ya', if true then String (if false then 'xxx' else 'y') + 'a'


# Returning if-else.
eq -1, do -> if 1 < 0.5 then 1 else -1


# `else`-less `if` Returns `undefined` with falsy condition.
eq void, do -> if 0 then
eq void, do -> return if 0 then


# As an implicit argument.
eq 100, Number if false then 300 else 100


# If-to-ternary with instanceof requires parentheses (false comment).
if {} instanceof Object
  ok true
else
  ok false


# If-to-ternary as part of a larger operation.
eq 2, 1 + if false then 10 else 1


# If/else indented within an assignment.
func = ->
  a =
    if false
      3
    else
      5
  101
  a
eq func(), 5


# Unmatched `then` should close implicit calls.
i = 1
if Boolean 1 then ++i
eq i, 2


# Unmatched `else` should close implicit blocks.
eq 2, do -> if 0 then -> 1 else 2


# With a suppressed indentation via assignment.
result =
  if      false then 10
  else if false    then 20
  else if 0     then 30
  else if NaN   then 40
  else               50 +
       if false then 10
       else          20

eq result, 70


if 'tight formatting with leading `then` is allowed'
then ok    'yay'
else throw 'boo'


# [coffee#738](https://github.com/jashkenas/coffee-script/issues/738)
eq 1, do if true then -> 1


eq '''
var that;
if (1) {
  if (that = 2) {
    if (3) {
      4;
    }
    if (that) {
      5;
    }
  }
}
if (!(that = 6)) {
  that;
}
''', Coco.compile '''
if 1
  if 2
    4 if 3
    5 if that
that unless 6
''', {+bare}


eq 2, [if 1 then 2       , 3].0
eq 2, [if 0 then 1 else 2, 3].0


# [coffee#1026](https://github.com/jashkenas/coffee-script/issues/1026)
throws "Parse error on line 3: Unexpected 'ELSE'", -> Coco.nodes '''
  if a then b
  else then c
  else then d
'''
