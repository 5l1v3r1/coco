fn = (first, ...rest) -> '' + rest
eq fn(1 to 5), '2,3,4,5'
eq fn(6 to 7), '7'

fn = (...heads, last) -> '' + heads
eq fn(1 to 5), '1,2,3,4'
eq fn(6 to 7), '6'

fn = (first, second, ...middles, last) -> '' + middles
eq fn(1 to 5), '3,4'
eq fn(6 to 7), ''


obj =
  name: 'moe'
  accessor: (...args) ->
    '' + [@name].concat args
  getNames: ->
    args = ['jane', 'ted']
    @accessor ...args
  index: 0
  0: {method: -> this is obj[0]}

eq obj.getNames(), 'moe,jane,ted'
ok obj[obj.index++].method(...[0]), 'should cache base value'


quartet = [0 to 3]
eq '0,1,2,3,42', '' + [
  ...quartet
  42
]
eq '42,0,1,2,3', '' + [
  42
  ...quartet
]


# Splats with `super`.
class Parent
  meth: (...args) -> args

class Child extends Parent
  meth: ->
    nums = [0, 1]
    super ...nums

eq '0,1', '' + (new Child).meth()


# Functions with splats being called with too few arguments.
pen = null
fun = (first, ...variable, penultimate, ultimate) -> pen := penultimate

fun 1 to 9
eq pen, 8

fun 1 to 3
eq pen, 2

fun 1 2
eq pen, 2


# Array splat expansions with assigns.
eq '0,1,2,3,4', '' + [a = 0, ...[1 2 3], b = 4]
eq a, 0
eq b, 4


o = x: {0}, (y = 1): {2}
{...x, ...(y)} = o
eq x[0], 0
eq y[2], 2
ok x is not o.x , 'should copy o.x'
ok y is not o[y], 'should copy o[y]'


throws 'splatting "new" on line 1' -> Coco.compile 'new C ...a'
throws 'splatting "new" on line 1' -> Coco.compile 'new C ...'


class Thisplat
  ->
    [me, [a0, a1, a2]] = @f ...
    eq me, this
    eq a0 * a2, 21
  f: -> [this, arguments]

class Thisplat2 extends Thisplat
  ~> super ...
  f: -> super ...

new Thisplat2 3 5 7


eq 0, [...[...[0]]][0]


[...onetwo, [], {}, five] = [1 to 5]
eq onetwo + '', '1,2'
eq five, 5


eq '0.0', 0.toFixed ...[1]


# Multiple splats in the same chain.
o =
  f: -> @a.push ...@@; this
  a: [1]

o.f(...o.a).f(...o.a)
eq '1,1,1,1' o.a + ''

(-> o.f(...).f(...))call o, 2
eq '1,1,1,1,2,2' o.a + ''


# [coffee#870](https://github.com/jashkenas/coffee-script/issues/870)
[...[], a] = [1]
eq a, 1

# `...` is same as `...[]`
[..., a] = [1 to 3]
eq a, 3

[a, ..., b] = [1 2]
eq a, 1
eq b, 2

[a, ..., b] = [1 to 5]
eq a, 1
eq b, 5

eq '''
(function(){
  var a;
  a = arguments[arguments.length - 1];
});
''', Coco.compile '(..., a) ->', {+bare}


# Don't call `__slice` on array literals.
eq '[a, a].concat([b]);' Coco.compile '[...[a]*2 b]' {+bare}
