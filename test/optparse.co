return unless require?

optparse = require './../lib/optparse'

flags =
  help   : ['display help']
  output : ['set output directory'   \DIR     ]
  nodejs : ['pass options to "node"' \ARGS+ \N]

result = optparse flags, <[ one two three -ho dir ]>

eq result.$args.length, 6
eq result.$args.4, \-o

result = optparse result.$flags, <[ --help -o folder one two ]>

eq result.help, true
eq result.output, 'folder'
eq result.$args + '', 'one,two'

result = optparse result.$flags, <[ -N h --nodejs debug -3 three ]>

ok result.nodejs instanceof Array
eq result.nodejs + '' ,'h,debug'
eq result.$args + ''  ,'three'
eq result.$unknowns.0 ,'-3'

eq \Options:\n + result, '''
Options:
  -h, --help          display help
  -o, --output DIR    set output directory
  -N, --nodejs ARGS+  pass options to "node"
'''
